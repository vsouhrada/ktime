apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'      // for intellij project
apply from: 'dependencies.gradle'
apply plugin: 'maven'

group 'com.github.vsouhrada'
version '0.1.0'

sourceCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.4"
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

// Gradle Wrapper Settings
task wrapper(type: Wrapper) {
    gradleVersion = rootProject.ext.gradleVersion
}

repositories {
    jcenter()
    maven { url project.getProperties().get("nexusRepo").toString() }
    maven { url project.getProperties().get("nexusSnapshotRepo").toString() }
    mavenCentral()
}

dependencies {
    def domainDependencies = rootProject.ext.domainDependencies

    // Kotlin
    compile domainDependencies.kotlinStdLib

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

idea {
    module {
        // https://youtrack.jetbrains.com/issue/IDEA-117540
        excludeDirs -= file("$buildDir")
        excludeDirs += file("$buildDir/dependency-cache")
        excludeDirs += file("$buildDir/libs")
        excludeDirs += file("$buildDir/tmp")
        excludeDirs += file("$buildDir/reports")
        excludeDirs += file("$buildDir/classes")
        excludeDirs += file("$buildDir/kotlin-classes")
        excludeDirs += file("$buildDir/libs")
        excludeDirs += file("$buildDir/test-results")
//        sourceDirs += file("$buildDir/generated/source/kapt/main/")
//        generatedSourceDirs += "$buildDir/generated/source/kapt/main/"
    }

}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        //beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: nexusRepo) {
            authentication(userName: nexusUsername, password: nexusPassword)
        }

        snapshotRepository(url: nexusSnapshotRepo) {
            authentication(userName: nexusUsername, password: nexusPassword)
        }

        pom.project {
            groupId 'com.github.vsouhrada'
            artifactId 'ktime'
            version version
            name 'ktime'
            description 'Kotlin extensions for Java 8 java.time API and others utilities with date and time'
            url 'https://github.com/vsouhrada/ktime'
            inceptionYear '2016'

//            scm {
//                connection 'scm:git:git://vsouhrada.github/srv/git/com.github.vsouhrada/ktime'
//            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'vsouhrada'
                    name 'Vaclav Souhrada'
                    email 'v.souhrada@gmail.com'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

task deploy(dependsOn: uploadArchives)